import planner.

% ================== 5.4 ====================================
main =>
    best_plan_unbounded([3, 3, south], Plan),
    foreach (Step in Plan)
        println(Step)
    end.

final([3, 3, north]) => true.

action([M, C, Bank], NextS, Action, Cost) =>
    member(BM, 0..M),
    member(BC, 0..C),
    BM + BC > 0, 
    BM + BC =< 2,
    OppBank = opposite(Bank),
    Action = $cross(BM, BC, OppBank), 
    Cost = 1,
    
    % 当前岸上剩余的传教士和食人族的数量
    NewM1 = M - BM,
    NewC1 = C - BC,

    % 对岸的传教士和食人族数量
    NewM2 = 3 - NewM1,
    NewC2 = 3 - NewC1,
    
    if NewM1 !== 0 then  % missionaries are safe
        NewM1 >= NewC1
    end,
    
    if NewM2 !== 0 then
        NewM2 >= NewC2
    end,
    
    NextS = [NewM2, NewC2, OppBank].

opposite(south) = north.
opposite(north) = south.

